#!/usr/bin/env perl
use Mojolicious::Lite;
use JSON;
# Documentation browser under "/perldoc"
plugin 'PODRenderer';

plugin 'CHI'=>{
	default => {
		driver => 'Memory',
		global => 1
	}
};

sub enc{
	my $s = shift;
	$s = [] unless $s;
	return to_json($s);
}
sub dec{
	my $s = shift;
	return [] unless $s;
	return from_json($s);
}

get '/' => sub {
	my $self = shift;
	my $ip = $self->tx->local_address;
	$self->render('index', ip=>$ip);
};

my $max = 100;
any '/#id/callback' => sub{
	my $self = shift;
	my $id = $self->param('id');
	my $msg = {};
	$msg->{action} = $self->param('action');
	$msg->{buddyId} = $self->param('buddyId');
	$msg->{groupId} = $self->param('groupId');
	$msg->{content} = $self->param('content');
	$msg->{time} = time();
	if( $msg->{action} ){
		my $arr = dec( $self->chi->get($id) );
		# 100개만 남기기
		my $len = @{$arr};
		if( $len > $max*1.5 ){
			$arr = [splice @{$arr},(-1*$max)];
		}
		push(@{$arr},$msg);
		$self->chi->set($id=>enc($arr));
		$self->render(text=>'ok');
	}
	else{
		$self->render(text=>'no action');
	}
};

get '/#id' => sub{
	my $self = shift;
	my $id = $self->param('id');
	my $data = $self->chi->get($id);
	$data = enc unless $data;
	$self->render(text=>$data);
};

get '/#id/flush' => sub{
	my $self = shift;
	my $id = $self->param('id');
	my $data = $self->chi->get($id);
	if( $data ){
		$self->chi->set($id=>enc);
	}
	else{
		$data = enc;
	}
	$self->render(text=>$data);
};

app->start;

__DATA__

@@ index.html.ep
% layout 'default';
% title '마이피플 봇 Callback Buffer';
<div class="page-header">
<h2><%= title %></h2>
<small>마이피플 봇 API를 이용할 때에는, 사용자의 메세지를 HTTP콜백으로 받기 위해서 Public IP를 가진 웹서버가 하나 필요합니다. 이 서비스는 콜백을 대신 받아서 임시로 저장해주는 서비스로, 따로 서버가 없더라도 마이피플 봇을 만들 수 있습니다. 받은 메세지들을 암호화해 주는 기능은 준비중입니다.</small>
</div>
<p>
<h3>이용방법</h3>
<ol>
	<li>사용하려는 봇의 아이디를 <span class='label label-info'>mybot.bot</span>이라 했을 때,  </li>
	<li>콜백주소를 <%= url_for('')->to_abs %><span class='label label-info'>mybot.bot</span>/callback 으로 지정하고, IP는 <%= $ip %> 로 설정합니다.
	<br>예시)<br>
<img src='<%= url_for('/img/callback_setting.png')->to_abs %>'>
	</li>
	<li><%= url_for('')->to_abs %><span class='label label-info'>mybot.bot</span> 에서 쌓인 메세지들을 <b>확인만</b> 할 수 있습니다.</li>
	<li><%= url_for('')->to_abs %><span class='label label-info'>mybot.bot</span>/flush 에서 쌓인 메세지들을 사용합니다. 조회 후 삭제됩니다. 봇에서는 이 주소를 이용해 쌓인 메세지들을 가져오도록 합니다.</li>
</ol>
</p>
<p>
	<h3>참고</h3>
	<ul>
		<li>MyPeople API : <a href='http://dna.daum.net/apis/mypeople'>http://dna.daum.net/apis/mypeople</a>
		<li>Source : <a href='https://github.com/sng2c/net-mypeople-bot'>https://github.com/sng2c/net-mypeople-bot</a></li>
	</ul>
</p>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head>
     <title><%= title %></title>
     <link rel="stylesheet" href="<%= url_for("/css/bootstrap.min.css") %>" />
     <script src="//ajax.googleapis.com/ajax/libs/jquery/1.9.1/jquery.min.js"></script>
     <script language="javascript" src="<%= url_for("/js/bootstrap.min.js") %>"></script>
  </head>
  <body>
    <div class="container">
       <%= content %>
    </div>
  </body>
</html>
